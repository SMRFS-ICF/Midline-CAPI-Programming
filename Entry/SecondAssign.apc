{Application 'SECONDASSIGN' logic file generated by CSPro}
PROC GLOBAL
numeric		xEAnum, xHHnum, xIntNum,i,j,k,lrc,ag,m;
string		yintnum;


PROC SECONDASSIGN_FF
preproc
xEAnum   = tonumber( sysparm()[1:3] );   { Cluster number }
xHHnum   = tonumber( sysparm()[4:3] );   { household number }
xIntNum  = tonumber( sysparm()[7:3] );   { interviewer number }
yIntNum  = sysparm()[7:3];

// delete pff file
filedelete(".\application.pff");

open(CTRL_HH);
H_CLUSTER = xEAnum;
H_HH = xHHnum;
if !loadcase(CTRL_HH) then
	 {ENG+} errmsg("Error accessing HH control file, process cancelled"); {+ENG}
	close(CTRL_HH);
	execpff(".\Interviewer.pff");
	stop(1);
endif;	


PROC SEC_INTERV
preproc

loadcase(INTERV,yintnum);

	 {ENG+} m = accept("Would you like to assign Interviewer B or the Ag Specialist?", "Assign Interviewer B", "Assign Ag Specialist","Assign both","Cancel and exit"); {+ENG}
	 if m in 0,4 then
	   execpff(".\Interviewer.pff");
	   stop(1);
	 elseif m in 2 then 
	   skip to AG_INTERV;
     endif;	
	

// check if 2nd interviewer was assigned previously
if H_INT2 <> notappl then
	loadcase(interv,H_INT2);
	{ENG+} 
	i = accept(concat("Interviewer B assigned previously to ",edit("999",H_INT2)," - ",strip(iname)),
		"Assign a new Interviewer B",
		"Cancel new assignment");
	 {+ENG}	
	if i in 0,2 then
		execpff(".\Interviewer.pff");
		stop(1);
	endif;	
endif;
i = ITEAM;
j = ISEX;
k = IROLE;

lrc = selcase(interv,"") include (interv.iname) where ITEAM = i & ICODE <> xIntnum & IROLE in 0,1;

if lrc = 0 then
	{ENG+} errmsg("Error in Interviewer B selection"); {+ENG}
	execpff(".\Interviewer.pff");
	stop(1);
endif;	

H_INT2 = icode;
if m = 1 then
  writecase(ctrl_hh);
endif;
{ENG+} errmsg("Interviewer B assigned to %s - %s",edit("999",icode),strip(iname)); {+ENG}

if m = 3 then 
  skip to AG_INTERV;
else
  close(ctrl_hh);
  execpff(".\Interviewer.pff");	
  stop(1);
endif;

PROC AG_INTERV
preproc


// check if Ag interviewer was assigned previously
if H_INT3 <> notappl then
	loadcase(INTERV, H_INT3);
	{ENG+} 
	i = accept(concat("Ag Interviewer assigned previously to ",edit("999",H_INT3)," - ",strip(iname)),
		"Assign a new Ag Interviewer",
		"Cancel new assignment");
	 {+ENG}
	if i in 0,2 then
		execpff(".\Interviewer.pff");
		stop(1);
	endif;	
endif;

loadcase(INTERV,yintnum);

i = ITEAM;
k = IROLE;

ag = selcase(INTERV,"") include (INTERV.INAME) where ITEAM = i & ICODE <> xIntnum & IROLE = 2;
	
if ag = 0 then
	{ENG+} errmsg("Error in Ag Interviewer selection"); {+ENG}
	execpff(".\Interviewer.pff");
	stop(1);
endif;	

H_INT3 = icode;
writecase(ctrl_hh);
{ENG+} errmsg("Ag Interviewer assigned to %s - %s",edit("999",icode),strip(iname)); {+ENG}
close(ctrl_hh);
execpff(".\Interviewer.pff");	
stop(1);
