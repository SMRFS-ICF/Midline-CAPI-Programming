{Application 'ADDHIDDEN' logic file generated by CSPro}
PROC GLOBAL
numeric 	 xHHnum, xEAnum;
numeric		i,j,k,l,lrc;
string		yHHnum, yEAnum;				{ keys in string }	


PROC ADDHIDDEN_FF
preproc
xEAnum   = tonumber( sysparm()[1:3] );   { Cluster number }
xHHnum   = tonumber( sysparm()[4:3] );   { household number }
yEAnum   = sysparm()[1:3];   { Cluster number }
yHHnum   = sysparm()[4:3];   { household number }

// delete pff file (activate after test)
filedelete(".\application.pff");

PROC H_ADDRESS
if length(strip($)) < 3 then
	{ENG+} errmsg("Address has to be completed"); {+ENG}
	reenter;
endif;	
PROC H_HEAD
if length(strip($)) < 3 then
	{ENG+} errmsg("Name of the Primary Decisionmaker has to be completed"); {+ENG}
	reenter;
endif;	

{ENG+} i = accept(concat("Confirm the new hidden household ",edit("999",xHHnum)), "Yes","No"); {+ENG}

if i = 1 then
	lrc = loadcase(CLUSTERS,yEAnum);
	if !lrc then
		errmsg(00001,yEAnum);
		execPFF("supervisor.pff",stop);
		stop(1);
	endif;
	
	lrc = loadcase(sampsel,yEAnum);
	if !lrc then
		errmsg(00002,yEAnum);
		execPFF("supervisor.pff",stop);
		stop(1);
	endif;
	
// Update clusters
	ytotHH = ytotHH+1;
	lrc = writecase(clusters);
	if lrc = 0 then
		{ENG+} errmsg("** Error ** writing clusters file"); {+ENG}
		execPFF("supervisor.pff",stop);
		stop(1);
	endif;
		
// Update sampsel	

	xtotal = xtotal + 1;
	xnumber(xtotal)  = xhhnum;
	xaddress(xtotal) = h_address;
	xname(xtotal)    = h_head;
	xresult(xtotal)  = 0;
	xintcode(xtotal) = 0;
	xintdate(xtotal) = 0;
	lrc = writecase(sampsel);
	if lrc = 0 then
		{ENG+} errmsg("** Error ** writing sampsel file"); {+ENG}
		execPFF("supervisor.pff",stop);
		stop(1);
	endif;
	
// update ctrl_cluster	
	lrc = loadcase(ctrl_cluster,yEAnum);
	if lrc & hh(1) <> notappl then
		hh(xtotal)			= xhhnum;
		enum(xtotal)		= 0;
		enum_name(xtotal)	= "Not assigned";
		Head_hh_name(xtotal)= h_head;
		HH_address(xtotal)	= h_address;
		lrc = writecase(ctrl_cluster);
		if lrc = 0 then
			{ENG+} errmsg("** Error ** writing ctrl_cluster file"); {+ENG}
			execPFF("supervisor.pff",stop);
			stop(1);
		endif;
	endif;
		
	{ENG+} errmsg("Hidden household %d added in EA %d",xhhnum,xEAnum); {+ENG} 
	execPFF("supervisor.pff",stop);
	stop(1);
else
	execPFF("supervisor.pff",stop);
	stop(1);
endif;
	
